#compdef zsh_snippets

_zsh_snippets() {
    local curcontext="$curcontext" state line

    _arguments -C \
        "1: :->action" \
        "2: :->shortcut"

    case $state in
        action)
            local -a actions=(
                "add:Create (or update) a snippet"
                "delete:Delete a snippet"
                "expand:Expand a snippet"
                "list:List snippets"
                "mode:Change jump mode"
                "help:Show the command help"
            )

            _describe 'action' actions
            ;;
        shortcut)
            case $line[1] in
                add|delete|expand)
                    _zsh_snippets_init

                    local -a snippets

                    for k in "${(@k)zshSnippets}"; do
                        snippets+=("$k:$zshSnippets[$k]")
                    done

                    _describe "shortcut" snippets
                    ;;
                mode)
                    local -a modes=(
                        "backward:Jump between placeholders from back of string"
                        "forward:Jump between placeholders from front of string"
                    )
                    _describe "mode" modes
            esac
            ;;
    esac
}
